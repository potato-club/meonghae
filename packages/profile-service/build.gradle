plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.8'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '6.8.0'
	//dsl
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
}

group = 'com.meonghae'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

//dsl설정
apply plugin: "com.ewerk.gradle.plugins.querydsl"
//
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url "https://www.datanucleus.org/downloads/maven2/"}
}

ext {
	set('springCloudVersion', "2021.0.5")
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'mysql:mysql-connector-java:8.0.32'
	//밑에 두줄 dsl설정
	implementation 'com.querydsl:querydsl-jpa'
	implementation 'com.querydsl:querydsl-apt'

	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'io.lettuce:lettuce-core:6.2.3.RELEASE'

	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'
	//rabbitMQ관련
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.retry:spring-retry'
	//FCM관련
	implementation 'com.google.firebase:firebase-admin:9.2.0'
	implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'
	//subnet관련
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'commons-net:commons-net:3.8.0'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

//dsl설정 -----
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
	library = "com.querydsl:querydsl-apt"
	jpa = true
	querydslSourcesDir = querydslDir
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', querydslDir]
		}
	}
}

compileQuerydsl{
	options.annotationProcessorPath = configurations.querydsl
}

configurations {
	querydsl.extendsFrom compileClasspath
}
//--------
springBoot {
	buildInfo()
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact bootJar
		}
	}
}
spotless {

	format 'misc', {
		// define the files to apply 'misc' to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}

	java {// to customize, go to https://github.com/diffplug/spotless/tree/main/plugin-gradle#java

		target 'src/*/java/**/*.java'

		// Use the default importOrder configuration
		importOrder()

		// Clean up
		removeUnusedImports()

		// Apply google-java-format formatter
		googleJavaFormat()
	}
}
